<p>The following code shows how to access the Column and Row coordinates of a dbl-clicked cell of a TDBGrid, and the string value of the cell contents.</p>

<p>As written, it displays the cell's Column and Row number + string contents on the form's caption.  Up to you what you actually do with these values.</p>

<p>It work because the dataset cursor on the dataset connected to the DBGrid is moved to the dataset row corresponding to the cell where the mouse pointer is.</p>

<pre><code>type
 TMyDBGrid = class(TDBGrid);

procedure TForm1.DBGrid1DblClick(Sender: TObject);
var
  ARow,
  ACol : Integer;
  Pt : TPoint;
  CellValue : String;
begin
  //  First, get the mouse pointer coordinates
  Pt.X := Mouse.CursorPos.X;
  Pt.Y := Mouse.CursorPos.Y;
  //  Translate them into the coordinate system of the DBGrid
  Pt := DBGrid1.ScreenToClient(Pt);

  //  Use TDBGrids inbuilt functionality to identify the Column and
  //  row number. 
  ACol := DBGrid1.MouseCoord(Pt.X, Pt.Y).X -1;
  ARow := DBGrid1.MouseCoord(Pt.X, Pt.Y).Y;
  CellValue := DBGrid1.Columns[ACol].Field.AsString;

  Caption := Format('Col:%d Row:%d Cell Value:%s', [ACol, ARow, CellValue]);
end;
</code></pre>

<p>Note that I've used the Caption property of the form to display the grid cell info just as a quick n dirty way of showing the information somewhere.  Of course you could equally well display it on another area of the form or somewhere on a different form entirely.  The above code will work equally well in the grid's <code>OnCellClick</code> event, btw.</p>

<p>As noted in a comment, you can use the grid's <code>SelectedField</code> property instead of the above, but personally I think the above is more instructive of how to work with a DBGrid, because it shows how to get the cell's Column and Row coordinates.  See the DBGrid's <code>SelectedField</code>, <code>SelectedIndex</code> and <code>SelectedRows</code> properties in the Online Help for more info on useful properties of the TDBGrid.</p>

<p><strong>Update</strong>  You asked in a comment for an example of showing the information on another form.  Let's suppose this form is called <code>OtherForm</code>, is in a unit <code>OtherFormu.Pas</code> and is created before the <code>DBGrid1DblClick</code> evenbt is called.  You need to use this unit in the Useslist of the unit which contains the DBGrid. Let's suppose this other form contains a TMemo control called Memo1.  Then, you could write your <code>DBGrid1DblClick</code> hanndler like this:</p>

<pre><code>procedure TForm1.DBGrid1DblClick(Sender: TObject);
[as above]
begin
  [ as above ]
  CellValue := DBGrid1.Columns[ACol].Field.AsString;

  OtherForm.Memo1.Lines.Add(Format('Col:%d Row:%d Cell Value:%s', [ACol, ARow, CellValue]));
end;
</code></pre>
