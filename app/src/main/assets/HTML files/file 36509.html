<p>You should sort the array itself with <a href="http://docwiki.embarcadero.com/Libraries/en/System.Generics.Collections.TArray.Sort" rel="nofollow"><code>System.Generics.Collections.TArray.Sort</code></a> and iterate over the array to populate the data to the listbox.</p>

<pre><code>uses
  System.Generics.Collections,
  System.Generics.Defaults,
  System.StrUtils;

procedure TmyForm.sortME( criteria: string; asc: Boolean );
var
  LCriteriaIndex : Integer;
  LData : TList&lt;randomObject&gt;;
begin
  LCriteriaIndex := IndexText( criteria, ['criteria1', 'criteria2', 'criteria3'] );

  if LCriteriaIndex &lt; 0 then
    raise EArgumentException.Create( 'unknown criteria' );

  LData := TList&lt;randomObject&gt;.Create( TComparer&lt;randomObject&gt;.Construct(
        function( const L, R : randomObject ) : Integer
    begin
      case LCriteriaIndex of
        0 :
          Result := CompareText( L.criteria1, R.criteria1 ); 
        1 :
          Result := CompareText( L.criteria2, R.criteria2 );
        2 :
          Result := CompareText( L.criteria3, R.criteria3 );
      end;

      if not asc
      then
        Result := -Result;
    end ) );

  LData.AddRange( AllRandomObjects );
  LData.Sort;

  showData( LData.ToArray );
end;

procedure TmyForm.showData( AData : TArray&lt;randomObject&gt; );
var
  LData : randomObject;
  LItem : TListBoxItem;
begin
  listbox.BeginUpdate;
  try
    listbox.Clear;
    for LData in AData do
    begin
      LItem := TListBoxItem.Create( listbox );

      //blah-blah, create text objects to add to the item for displaying

      listbox.AddObject( LItem );
    end;
  finally
    listbox.EndUpdate;
  end;
end;
</code></pre>
