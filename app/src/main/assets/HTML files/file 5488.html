<p><strong>What you are seeking to do is called <a href="http://msdn.microsoft.com/en-us/library/ms379613.aspx" rel="nofollow">"operator overloading"</a>, which allows you to define comparison and mathematical operators for complex types</strong> (like your class <code>Something</code>).</p>

<p>For example, you can overload the addition and subtraction operators from within your <code>Something</code> class like this:</p>

<pre><code>Public Shared Operator +(ByVal val1 As Something, ByVal val2 As Something) As Something
    ''#(calculate the sum of the two specified values)
    Return New Something(val1.SomeBigNumber + val2.SomeBigNumber)
End Operator

Public Shared Operator -(ByVal val1 As Something, ByVal val2 As Something) As Something
    ''#(calculate the difference of the two specified values)
    Return New Something(val1.SomeBigNumber - val2.SomeBigNumber)
End Operator
</code></pre>

<p>And then you can write code like:</p>

<pre><code>Dim newValue As Something = something1 + something2
</code></pre>

<p><br>
You can also overload the comparison operators (greater than, less than, equal to, and everything in between) in almost exactly the same way:</p>

<pre><code>Public Shared Operator &gt;(ByVal val1 As Something, ByVal val2 As Something) As Boolean
    ''#(return True if the first value is larger, False otherwise)
    Return (val1.SomeBigNumber &gt; val2.SomeBigNumber)
End Operator

Public Shared Operator &lt;(ByVal val1 As Something, ByVal val2 As Something) As Boolean
    ''#(return True if the first value is smaller, False otherwise)
    Return (val1.SomeBigNumber &lt; val2.SomeBigNumber)
End Operator
</code></pre>

<p>Allowing you to write code like:</p>

<pre><code>If something1 &gt; something2 Then
    MesssageBox.Show("The first value is larger.")
Else
    MessageBox.Show("The second value is larger.")
End If
</code></pre>

<p>Note, however, that some of these operators must be overloaded in <em>pairs</em>. Specifically:</p>

<ul>
<li><code>=</code> and <code>&lt;&gt;</code></li>
<li><code>&gt;</code> and <code>&lt;</code></li>
<li><code>&gt;=</code> and <code>&lt;=</code></li>
<li><code>IsTrue</code> and <code>IsFalse</code></li>
</ul>
