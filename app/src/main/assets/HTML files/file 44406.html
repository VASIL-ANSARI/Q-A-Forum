<p>To avoid security hassles, I used a .HTA for this demo:</p>

<pre><code>&lt;html&gt;
 &lt;head&gt;
  &lt;title&gt;SQL DropDown Demo&lt;/title&gt;
  &lt;hta:application
     id="demo"
  &gt;&lt;/hta&gt;
  &lt;script type="text/vbscript"&gt;

Option Explicit

Sub Window_OnLoad
  Dim oDD : Set oDD = document.GetElementById("ddSQL")
  Dim oDb : Set oDb = CreateObject("ADODB.Connection")
  oDb.Open "valid connection string"
  Dim sQL : sQL     = "valid SQL query"
  Dim oRS : Set oRS = oDb.Execute(sQL)
  Dim oOpt
  Do Until oRS.EOF
      Set oOpt = oDD.document.createElement("option")
      oOpt.Text  = oRS.Fields("valid column name").Value
      oOpt.Value = oOpt.Text
      oDD.Options.Add oOpt
      oRS.MoveNext
  Loop
End Sub

  &lt;/script&gt;
 &lt;/head&gt;
 &lt;body&gt;
  &lt;form&gt;
   &lt;select id="ddSQL"&gt;
   &lt;/select&gt;
  &lt;/form&gt;
 &lt;/body&gt;
&lt;/html&gt;
</code></pre>

<p>Evidence:</p>

<p><img src="http://i.stack.imgur.com/67Awm.png" alt="Evidence"></p>

<p>Key concepts: empty select element, using the resultset loop to create options, setting their properties, adding them to the select.</p>
