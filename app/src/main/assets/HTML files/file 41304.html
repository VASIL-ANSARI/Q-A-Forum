<p><strong><em>(edited)</em></strong></p>

<p>I would suggest you try a different approach. The following stylesheet:</p>

<p><strong>XSLT 1.0</strong></p>

<pre><code>&lt;xsl:stylesheet version="1.0"
xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
xmlns:exsl="http://exslt.org/common"
extension-element-prefixes="exsl"&gt;
&lt;xsl:output method="xml" version="1.0" encoding="UTF-8" indent="yes"/&gt;

&lt;xsl:variable name="categories"&gt;
    &lt;category row="1"&gt;Agent Voicemail / Message&lt;/category&gt;
    &lt;category row="1"&gt;Did Not Leave Voicemail&lt;/category&gt;
    &lt;category row="1"&gt;Information Provided&lt;/category&gt;
    &lt;category row="1"&gt;Lost Opportunity&lt;/category&gt;
    &lt;category row="1"&gt;No Agent Available&lt;/category&gt;
    &lt;category row="1"&gt;Ring No Answer&lt;/category&gt;
    &lt;category row="2"&gt;SIT Tone&lt;/category&gt;
    &lt;category row="2"&gt;Tour / Sale / Booking&lt;/category&gt;
    &lt;category row="2"&gt;Non-Sales Related&lt;/category&gt;
    &lt;category row="2"&gt;Other&lt;/category&gt;
    &lt;category row="2"&gt;Updated Existing Enquiry&lt;/category&gt;
&lt;/xsl:variable&gt;

&lt;xsl:variable name="col-set" select="exsl:node-set($categories)/category" /&gt;

&lt;xsl:template match="/"&gt;
    &lt;xsl:for-each select="DialStats/Countries"&gt;
        &lt;xsl:variable name="country" select="." /&gt;
        &lt;table border="1" style="width:30%; display:inline-block"&gt;
            &lt;tr&gt;
                &lt;th colspan="{count($col-set[@row='1'])}"&gt;
                    &lt;xsl:value-of select="$country/@Country"/&gt;
                &lt;/th&gt;
            &lt;/tr&gt;
            &lt;tr&gt;
                &lt;xsl:for-each select="$col-set[@row='1']"&gt;
                    &lt;th&gt;&lt;xsl:value-of select="."/&gt;&lt;/th&gt;
                &lt;/xsl:for-each&gt;
            &lt;/tr&gt;
            &lt;tr&gt;
                &lt;xsl:for-each select="$col-set[@row='1']"&gt;
                    &lt;td&gt;
                        &lt;xsl:value-of select="$country/Products/Dispositions[@Result=current()]/@Count"/&gt;
                    &lt;/td&gt;
                &lt;/xsl:for-each&gt;
            &lt;/tr&gt;
            &lt;tr&gt;
                &lt;xsl:for-each select="$col-set[@row='2']"&gt;
                    &lt;th&gt;&lt;xsl:value-of select="."/&gt;&lt;/th&gt;
                &lt;/xsl:for-each&gt;
            &lt;/tr&gt;
            &lt;tr&gt;
                &lt;xsl:for-each select="$col-set[@row='2']"&gt;
                    &lt;td&gt;
                        &lt;xsl:value-of select="$country/Products/Dispositions[@Result=current()]/@Count"/&gt;
                    &lt;/td&gt;
                &lt;/xsl:for-each&gt;
            &lt;/tr&gt;
        &lt;/table&gt;
    &lt;/xsl:for-each&gt;
&lt;/xsl:template&gt;

&lt;/xsl:stylesheet&gt;
</code></pre>

<p>when applied to your input example, will produce:</p>

<pre><code>&lt;?xml version="1.0" encoding="UTF-8"?&gt;
&lt;table border="1" style="width:30%; display:inline-block"&gt;
   &lt;tr&gt;
      &lt;th colspan="6"&gt;France&lt;/th&gt;
   &lt;/tr&gt;
   &lt;tr&gt;
      &lt;th&gt;Agent Voicemail / Message&lt;/th&gt;
      &lt;th&gt;Did Not Leave Voicemail&lt;/th&gt;
      &lt;th&gt;Information Provided&lt;/th&gt;
      &lt;th&gt;Lost Opportunity&lt;/th&gt;
      &lt;th&gt;No Agent Available&lt;/th&gt;
      &lt;th&gt;Ring No Answer&lt;/th&gt;
   &lt;/tr&gt;
   &lt;tr&gt;
      &lt;td&gt;11&lt;/td&gt;
      &lt;td&gt;37&lt;/td&gt;
      &lt;td&gt;13&lt;/td&gt;
      &lt;td&gt;1&lt;/td&gt;
      &lt;td&gt;1&lt;/td&gt;
      &lt;td&gt;6&lt;/td&gt;
   &lt;/tr&gt;
   &lt;tr&gt;
      &lt;th&gt;SIT Tone&lt;/th&gt;
      &lt;th&gt;Tour / Sale / Booking&lt;/th&gt;
      &lt;th&gt;Non-Sales Related&lt;/th&gt;
      &lt;th&gt;Other&lt;/th&gt;
      &lt;th&gt;Updated Existing Enquiry&lt;/th&gt;
   &lt;/tr&gt;
   &lt;tr&gt;
      &lt;td&gt;3&lt;/td&gt;
      &lt;td&gt;7&lt;/td&gt;
      &lt;td/&gt;
      &lt;td/&gt;
      &lt;td/&gt;
   &lt;/tr&gt;
&lt;/table&gt;
&lt;table border="1" style="width:30%; display:inline-block"&gt;
   &lt;tr&gt;
      &lt;th colspan="6"&gt;Italy&lt;/th&gt;
   &lt;/tr&gt;
   &lt;tr&gt;
      &lt;th&gt;Agent Voicemail / Message&lt;/th&gt;
      &lt;th&gt;Did Not Leave Voicemail&lt;/th&gt;
      &lt;th&gt;Information Provided&lt;/th&gt;
      &lt;th&gt;Lost Opportunity&lt;/th&gt;
      &lt;th&gt;No Agent Available&lt;/th&gt;
      &lt;th&gt;Ring No Answer&lt;/th&gt;
   &lt;/tr&gt;
   &lt;tr&gt;
      &lt;td/&gt;
      &lt;td&gt;1&lt;/td&gt;
      &lt;td&gt;6&lt;/td&gt;
      &lt;td/&gt;
      &lt;td/&gt;
      &lt;td&gt;7&lt;/td&gt;
   &lt;/tr&gt;
   &lt;tr&gt;
      &lt;th&gt;SIT Tone&lt;/th&gt;
      &lt;th&gt;Tour / Sale / Booking&lt;/th&gt;
      &lt;th&gt;Non-Sales Related&lt;/th&gt;
      &lt;th&gt;Other&lt;/th&gt;
      &lt;th&gt;Updated Existing Enquiry&lt;/th&gt;
   &lt;/tr&gt;
   &lt;tr&gt;
      &lt;td&gt;5&lt;/td&gt;
      &lt;td/&gt;
      &lt;td&gt;1&lt;/td&gt;
      &lt;td&gt;6&lt;/td&gt;
      &lt;td&gt;13&lt;/td&gt;
   &lt;/tr&gt;
&lt;/table&gt;
&lt;table border="1" style="width:30%; display:inline-block"&gt;
   &lt;tr&gt;
      &lt;th colspan="6"&gt;Netherlands&lt;/th&gt;
   &lt;/tr&gt;
   &lt;tr&gt;
      &lt;th&gt;Agent Voicemail / Message&lt;/th&gt;
      &lt;th&gt;Did Not Leave Voicemail&lt;/th&gt;
      &lt;th&gt;Information Provided&lt;/th&gt;
      &lt;th&gt;Lost Opportunity&lt;/th&gt;
      &lt;th&gt;No Agent Available&lt;/th&gt;
      &lt;th&gt;Ring No Answer&lt;/th&gt;
   &lt;/tr&gt;
   &lt;tr&gt;
      &lt;td&gt;1&lt;/td&gt;
      &lt;td/&gt;
      &lt;td&gt;17&lt;/td&gt;
      &lt;td/&gt;
      &lt;td/&gt;
      &lt;td&gt;3&lt;/td&gt;
   &lt;/tr&gt;
   &lt;tr&gt;
      &lt;th&gt;SIT Tone&lt;/th&gt;
      &lt;th&gt;Tour / Sale / Booking&lt;/th&gt;
      &lt;th&gt;Non-Sales Related&lt;/th&gt;
      &lt;th&gt;Other&lt;/th&gt;
      &lt;th&gt;Updated Existing Enquiry&lt;/th&gt;
   &lt;/tr&gt;
   &lt;tr&gt;
      &lt;td/&gt;
      &lt;td&gt;2&lt;/td&gt;
      &lt;td/&gt;
      &lt;td/&gt;
      &lt;td/&gt;
   &lt;/tr&gt;
&lt;/table&gt;
</code></pre>

<p>rendered as:</p>

<p><img src="http://i.stack.imgur.com/Bp7uf.png" alt="enter image description here"></p>

<p>The table's structure is constant and does not depend on the incoming data.</p>
