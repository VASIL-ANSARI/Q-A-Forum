<p>You can define a character map that maps the characters not allowed to one allowed, for instance a space:</p>

<pre><code>&lt;xsl:output indent="yes" method="html" use-character-maps="m1"/&gt;

&lt;xsl:character-map name="m1"&gt;
  &lt;xsl:output-character character="&amp;#150;" string=" "/&gt;
&lt;/xsl:character-map&gt;
</code></pre>

<p>As an alternative, use a template replacing all illegal characters, according to <a href="http://www.w3.org/TR/xslt-xquery-serialization/#HTML_CHARDATA" rel="nofollow">http://www.w3.org/TR/xslt-xquery-serialization/#HTML_CHARDATA</a> these are control characters #x7F-#x9F so using</p>

<pre><code>&lt;xsl:template match="text()"&gt;
  &lt;xsl:value-of select="replace(., '[&amp;#x007F;-&amp;#x009F;]', ' ')"/&gt;
&lt;/xsl:template&gt;
</code></pre>

<p>should make sure those characters in text nodes in the input document are replaced by a spaces.</p>

<p>As another alternative, you could consider to output XHTML with elements in the XHTML namespaces and output method <code>xhtml</code>.</p>

<p>Based on the list of characters, a full character map mapping all illegal control characters to a space is</p>

<pre><code>&lt;xsl:character-map
                   name="no-control-characters"&gt;
   &lt;xsl:output-character character="&amp;#127;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#128;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#129;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#130;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#131;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#132;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#133;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#134;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#135;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#136;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#137;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#138;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#139;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#140;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#141;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#142;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#143;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#144;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#145;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#146;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#147;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#148;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#149;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#150;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#151;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#152;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#153;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#154;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#155;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#156;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#157;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#158;" string=" "/&gt;
   &lt;xsl:output-character character="&amp;#159;" string=" "/&gt;
&lt;/xsl:character-map&gt;
</code></pre>

<p>I generated that list with XSLT 2.0 and Saxon, using</p>

<pre><code>&lt;xsl:stylesheet version="2.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:xs="http://www.w3.org/2001/XMLSchema"
  xmlns:axsl="http://www.w3.org/1999/XSL/TransformAlias"
  exclude-result-prefixes="xs axsl"&gt;

&lt;xsl:param name="start" as="xs:integer" select="127"/&gt;
&lt;xsl:param name="end" as="xs:integer" select="159"/&gt;

&lt;xsl:param name="replacement" as="xs:string" select="' '"/&gt;

&lt;xsl:namespace-alias stylesheet-prefix="axsl" result-prefix="xsl"/&gt;

&lt;xsl:output method="xml" indent="yes" use-character-maps="character-reference"/&gt;

&lt;xsl:character-map name="character-reference"&gt;
  &lt;xsl:output-character character="Â«" string="&amp;amp;"/&gt;
&lt;/xsl:character-map&gt;

&lt;xsl:template name="main"&gt;
  &lt;axsl:character-map name="no-control-characters"&gt;
    &lt;xsl:for-each select="$start to $end"&gt;
      &lt;axsl:output-character character="Â«#{.};" string="{$replacement}"/&gt;
    &lt;/xsl:for-each&gt;
  &lt;/axsl:character-map&gt;
&lt;/xsl:template&gt;

&lt;/xsl:stylesheet&gt;
</code></pre>
